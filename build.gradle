plugins {
  id 'base'
}

group 'com.github.daggerok'
version '2.0.0'

wrapper {
  gradleVersion = '4.8'
  distributionType = 'BIN'
}

['bash', 'fish'].each { type ->

  def fqdn = "# ${project.group}:${project.name}:${project.version} #"
  def separator = '#' * fqdn.length()

  build.doLast {
    file("$projectDir/main.$type").text =
        file("$projectDir/src/$type".replaceAll('\\\\', '/'))
            .listFiles()
            .findAll { it.name.endsWith(".$type") }
            .collect { it.text }
            .join("${separator}\n") +
      """\
         |${separator}
         |${fqdn}
         |${separator}
         |""".stripMargin()
  }

  clean.doFirst {
     delete "$projectDir/$type"
  }
}

defaultTasks 'clean', 'build'
